plugins {
  id 'multiloader-loader'
  id 'fabric-loom'
}

repositories {
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
}

dependencies {
  minecraft "com.mojang:minecraft:${minecraft_version}"
  mappings loom.layered {
    officialMojangMappings()
    parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
  }
  modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
  modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

  modImplementation("com.terraformersmc:modmenu:17.0.0-beta.1")
}

loom {
  def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
  if (aw.exists()) {
    accessWidenerPath.set(aw)
  }
  mixin {
    defaultRefmapName.set("${mod_id}.refmap.json")
  }
  splitEnvironmentSourceSets()
  mods {
   "${mod_id}" {
    sourceSet sourceSets.main
    sourceSet sourceSets.client
   }
  }
  runs {
    client {
      client()
      setConfigName('Fabric Client')
      ideConfigGenerated(true)
      runDir('../runs/fabric/client')
    }
    server {
      server()
      setConfigName('Fabric Server')
      ideConfigGenerated(true)
      runDir('../runs/fabric/server')
    }
  }
}

// Implement mcgradleconventions loader attribute
def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements', 'includeInternal', 'modCompileClasspath'].each { variant ->
  configurations.named("$variant") {
    attributes {
      attribute(loaderAttribute, 'fabric')
    }
  }
}
sourceSets.configureEach {
  [it.compileClasspathConfigurationName, it.runtimeClasspathConfigurationName].each { variant->
    configurations.named("$variant") {
      attributes {
        attribute(loaderAttribute, 'fabric')
      }
    }
  }
}
loom.remapConfigurations.configureEach {
  configurations.named(it.name) {
    attributes {
      attribute(loaderAttribute, 'fabric')
    }
  }
}